# spinalcord
# https://github.com/spinalcord/animation-tree-tree-godot
# 
# This file is part of animation-tree-tree-godot.
# 
# This program is free software: you can redistribute it and/or modify
# it under the terms of the GNU Affero General Public.
#
# NOTE: The output generated by this addon is not affected by this license.

class_name DependencyContainer
extends RefCounted

var _definitions: Dictionary = {}
var _instances: Dictionary = {}

func bind(id: String, value_or_factory, singleton: bool = true) -> DependencyContainer:
	if value_or_factory is Callable:
		_definitions[id] = {"factory": value_or_factory, "singleton": singleton}
	else:
		_definitions[id] = {"factory": func(): return value_or_factory, "singleton": true}
	return self

func grab(id: String):
	if not _definitions.has(id):
		push_error("Service '%s' not found" % id)
		return null
	
	var definition = _definitions[id]
	
	if definition.singleton and _instances.has(id):
		return _instances[id]
	
	var inst = definition.factory.call()
	
	if definition.singleton:
		_instances[id] = inst
	
	return inst

func has(id: String) -> bool:
	return _definitions.has(id)

func remove(id: String) -> DependencyContainer:
	_definitions.erase(id)
	_instances.erase(id)
	return self
