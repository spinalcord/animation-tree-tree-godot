# spinalcord
# https://github.com/spinalcord/animation-tree-tree-godot
# 
# This file is part of animation-tree-tree-godot.
# 
# This program is free software: you can redistribute it and/or modify
# it under the terms of the GNU Affero General Public.
#
# NOTE: The output generated by this addon is not affected by this license.

class_name MetadataUtils

static func create_node_metadata(
	path: String, 
	node_type: String, 
	node_name: String, 
	has_children: bool,
	blend_point_index: int = -1,
	blend_position: Variant = null
) -> Dictionary:
	# Validate blend point parameters
	if blend_point_index >= 0 and blend_position == null:
		push_error("blend_position must be provided when blend_point_index is set")
		return {}
	
	if blend_point_index < 0 and blend_position != null:
		push_error("blend_point_index must be provided when blend_position is set")
		return {}
	
	var metadata = {
		"path": path,
		"node_type": node_type,
		"node_name": node_name,
		"has_children": has_children
	}
	
	# Only add blend point data if provided
	if blend_point_index >= 0:
		metadata["blend_point_index"] = blend_point_index
		metadata["blend_position"] = blend_position
	
	return metadata

static func get_path_from_metadata(metadata) -> String:
	if metadata is Dictionary:
		return metadata.get("path", "")
	else:
		return str(metadata) if metadata != null else ""

static func is_blend_point(metadata) -> bool:
	if metadata is Dictionary:
		return metadata.has("blend_point_index")
	return false

static func get_blend_point_index(metadata) -> int:
	if metadata is Dictionary:
		return metadata.get("blend_point_index", -1)
	return -1

static func get_blend_position(metadata) -> Variant:
	if metadata is Dictionary:
		return metadata.get("blend_position", null)
	return null
